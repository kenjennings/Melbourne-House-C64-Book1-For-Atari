14 GRAPHICS 8+16:REM The +16 eliminates text window.
15 COLOR 1:REM And set the pixel drawing color.
32 SETCOLOR 4,0,0:REM Border black.
33 SETCOLOR 2,0,15:REM Background white
35 REM Note that Atari doesn't have color map.  The drawn pixel 
36 REM color is set by color register.  Here, set it to black:
37 SETCOLOR 1,0,0
81 REM Initialize Delta variables. Redo if they both end up 0.
82 X=79:Y=49:DX=INT(RND(0)*3-1):DY=INT(RND(0)*3-1):IF DX=0 AND DY=0 THEN GOTO 82
91 REM Plotting pixels.  
95 PLOT X,Y:PLOT 319-X,Y:PLOT 319-X,191-Y:PLOT X,191-Y
101 REM More math and more plotting calls.
102 Y1=Y*2:X1=X*2:PLOT X1,Y1:Y1=191-Y1:X1=319-X1:PLOT X1,Y1
111 REM Increment deltas and fix horizontal boundary collision.
112 X=X+DX:Y=Y+DY:IF X<0 OR X>159 THEN DX=-DX:GOTO 112
121 REM Fix vertical boundary collision 
122 IF Y<0 OR Y>95 THEN DY=-DY:GOTO 112
131 REM X Random direction change.
132 IF RND(0)>0.9 THEN DX=INT(RND(0)*3-1)
141 REM Y Random direction change.
142 IF RND(0)>0.9 THEN DY=INT(RND(0)*3-1)
151 REM If Deltas are non-zero, then go plot again.
152 IF DX<>0 OR DY<>0 THEN GOTO 95
161 REM They were 0, so try to update them again.  It has to work eventually.
162 DX=INT(RND(0)*3-1):DY=INT(RND(0)*3-1):IF DX=0 AND DY=0 THEN GOTO 162
171 REM Loop back to plot next group of points.
172 GOTO 95
