0 Fast 
100 Graphics 8+16:Rem The +16 eliminates text window.
105 Color 1:Rem And set the pixel drawing color.
110 Setcolor 4,0,0:Rem Border black.
115 Setcolor 2,0,15:Rem Background white
120 Rem Note that Atari doesn't have color map.  The drawn pixel 
125 Rem color is set by color register.  Here, set it to black:
130 Setcolor 1,0,0
135 Rem Initialize Delta variables. Redo if they both end up 0.
140 X=79:Y=49
145 Dx=Int(Rnd(0)*3-1):Dy=Int(Rnd(0)*3-1)
150 If Dx=0 And Dy=0 Then Goto 145
155 Rem Plotting pixels.  
160 X2=319-X:Y2=191-Y
165 Plot X,Y:Plot X2,Y:Plot X2,Y2:Plot X,Y2
170 Rem More math and more plotting calls.
175 Y1=Y+Y:X1=X+X:Plot X1,Y1:Y1=191-Y1:X1=319-X1:Plot X1,Y1
180 Rem Increment deltas and fix horizontal boundary collision.
185 X=X+Dx:Y=Y+Dy:If X<0 Or X>159 Then Dx=-Dx:Goto 185
190 Rem Fix vertical boundary collision 
195 If Y<0 Or Y>95 Then Dy=-Dy:Goto 185
200 Rem X Random direction change.
205 If Rnd(0)>0.9 Then Dx=Int(Rnd(0)*3-1)
210 Rem Y Random direction change.
215 If Rnd(0)>0.9 Then Dy=Int(Rnd(0)*3-1)
220 Rem If Deltas are non-zero, then go plot again.
225 If Dx<>0 Or Dy<>0 Then Goto 160
230 Rem They were 0, so try to update them again.  It has to work eventually.
235 Dx=Int(Rnd(0)*3-1):Dy=Int(Rnd(0)*3-1)
240 If Dx=0 And Dy=0 Then Goto 235
245 Rem Loop back to plot next group of points.
250 Goto 160
